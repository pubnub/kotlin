buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.18.0"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.30'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java-library'
    id 'java-test-fixtures'
    id "org.jlleitschuh.gradle.ktlint" version "10.0.0"
    id "jacoco"
    id 'org.jetbrains.dokka' version '0.10.1'
    id 'org.unbroken-dome.test-sets' version '3.0.1'
    id "com.github.ben-manes.versions" version '0.38.0'
}

apply plugin: "com.vanniktech.maven.publish"

repositories {
    mavenCentral()
    jcenter()
}

testSets {
    integrationTest
}

sourceSets {
    test {
        resources {
            srcDir 'src/testFixtures/resources'
        }
    }
    integrationTest {
        resources {
            srcDir 'src/testFixtures/resources'
        }
    }
}

configurations {
    allTest
    testFixturesImplementation { extendsFrom allTest }
    integrationTestImplementation { extendsFrom allTest }
    testImplementation { extendsFrom allTest }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "com.squareup.retrofit2:retrofit:2.9.0"

    api "com.squareup.okhttp3:logging-interceptor:3.14.9"

    api 'com.google.code.gson:gson:2.8.6'
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.13.2'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.2'
    implementation "org.slf4j:slf4j-api:1.7.30"

    implementation "org.json:json:20210307"

    allTest "junit:junit:4.13.2"
    allTest "org.awaitility:awaitility-kotlin:4.0.3"
    allTest "com.github.tomakehurst:wiremock:2.27.2"

    allTest "ch.qos.logback:logback-classic:1.2.3"
    allTest "ch.qos.logback:logback-core:1.2.3"
    allTest "org.json:json:20210307"
    allTest 'org.aeonbits.owner:owner:1.0.12'

    testImplementation "io.mockk:mockk:1.11.0"
    testImplementation group: 'io.cucumber', name: 'cucumber-java', version: '6.10.4'
    testImplementation group: 'io.cucumber', name: 'cucumber-junit', version: '6.10.4'
    testImplementation group: 'io.cucumber', name: 'cucumber-picocontainer', version: '6.10.4'
}

shadowJar {
    classifier = "all"
    dependencies {
        exclude(dependency('org.jetbrains.kotlin:.*'))
    }
    dependencies {
        exclude(dependency('org.jetbrains:annotations:.*'))
    }
}

task cucumber(type: Test) {
    systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")
    systemProperty "cucumber.features", System.getProperty("cucumber.features")
    systemProperty "cucumber.plugins", System.getProperty("cucumber.plugins")
}

test {
    failFast = true
    exclude '**/contract/*.class'
}

dokka {
    outputFormat = 'gfm'
    outputDirectory = "$buildDir/dokka"
    configuration {
        includeNonPublic = false
        skipEmptyPackages = true
    }
}

ktlint {
    verbose = true
    additionalEditorconfigFile = file("check/.editorconfig")
    kotlinScriptAdditionalPaths {
        include fileTree("src/integrationTest/")
        include fileTree("src/testFixtures/")
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


build.finalizedBy(dokka, shadowJar)
